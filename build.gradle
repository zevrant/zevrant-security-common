plugins {
    id 'org.springframework.boot' version '2.1.4.RELEASE'
    id 'java'
    id 'maven-publish'
}

apply plugin: 'io.spring.dependency-management'

group = 'net.zevrant-services'
if(project.hasProperty('projVersion')) {
    project.version = project.projVersion
    ext.isSnapshot=false
} else {
    project.version = '0.0.1-snapshot'
}

sourceCompatibility = '1.8'
bootJar.enabled = false

repositories {
    mavenCentral()
}

ext {
    set('springCloudVersion', 'Greenwich.SR1')
}

publishing {
    def accessKeyProperty = System.getenv().get("ACCESS_KEY")
    def secretKeyProperty = System.getenv().get("SECRET_KEY")

    publications {
        myPublication(MavenPublication) {
            groupId 'net.zevrant-services'
            artifactId 'zevrant-security-common'

            from components.java
        }
    }
    repositories {
        maven {
            url "s3://zevrant-artifact-store/"
            credentials(AwsCredentials) {
                accessKey = accessKeyProperty
                secretKey = secretKeyProperty
            }
        }
    }
}

jar {
    enabled = true
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-ldap'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.cloud:spring-cloud-starter-aws'
    implementation 'com.amazonaws:aws-java-sdk-acm:1.11.648'
    implementation 'com.amazonaws:aws-java-sdk-core:1.11.648'
    implementation 'org.springframework.cloud:spring-cloud-aws-secrets-manager-config'
    implementation 'commons-lang:commons-lang:2.6'
    implementation 'org.bouncycastle:bcprov-jdk15on:1.63'
    implementation 'javax.xml.bind:jaxb-api:2.3.1'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

test {

}

publish.dependsOn assemble
